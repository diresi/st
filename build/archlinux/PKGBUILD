pkgname="st-git"
_pkgname="st"
pkgver=0.8.2.r7.g313f4139251d
pkgrel=1
pkgdesc="Simple terminal emulator by suckless.org with some patches."
arch=('i686' 'x86_64' 'armv7h' 'armv7l')
url="https://st.suckless.org/"
license=('MIT')
depends=('libxft')
makedepends=('git' 'ncurses' 'libxext')
optdepends=('xorg-fonts-misc')
source=("st::git+https://github.com/diresi/st.git")
md5sums=('SKIP')

provides=("$_pkgname")
conflicts=("$_pkgname")

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "${_pkgname}"
  # skip terminfo which conflicts with ncurses
  sed -i '/tic /,+1d' Makefile
}

build() {
  cd "$srcdir/$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$_pkgname"
  make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
